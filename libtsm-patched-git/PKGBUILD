# Maintainer: Aetf <7437103@gmail.com>
pkgname=libtsm-patched-git
_gitname=libtsm
pkgver=3.r17.gb73acb4
pkgrel=2
pkgdesc="Terminal-emulator State Machine"
arch=('i686' 'x86_64')
url="http://www.fredesktop.org/wiki/Software/kmscon/$_gitname"
license=('MIT')
depends=(glibc)
makedepends=('git' 'libxkbcommon')
provides=('libtsm')
conflicts=('libtsm')
options=(!libtool)
source=('git://people.freedesktop.org/~dvdhrm/libtsm'
        'addon-underline.patch'
        'addon-soft-black.patch'
        'addon-true-color.patch')
sha256sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')

pkgver() {
  cd "$_gitname"
  git describe --long | sed -r "s/^$_gitname-//;s/([^-]*-g)/r\\1/;s/-/./g"
}

build() {
  cd "$srcdir/$_gitname"
  echo 'Apply patch addon-underline.patch'
  patch -p1 -i ../addon-underline.patch
  echo 'Apply patch addon-soft-black.patch'
  patch -p1 -i ../addon-soft-black.patch
  echo 'Apply patch addon-true-color.patch'
  patch -p1 -i ../addon-true-color.patch

  test -f ./configure || NOCONFIGURE=1 ./autogen.sh
  ./configure --prefix=/usr
  make
}

package() {
  cd "$srcdir/$_gitname"
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$_gitname/COPYING"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
