# $Id: PKGBUILD 245243 2015-09-03 17:30:25Z anatolik $
# Maintainer: Aetf <aetf at unlimitedcodeworks.xyz>

pkgname='vim-python3-noruby'
pkgdesc='Vi Improved, a highly configurable, improved version of the vi text editor (with support for additional scripting languages)'
provides=("vim=${pkgver}-${pkgrel}")
_topver=7.4
_patchlevel=854
_tag=v${_topver/./-}-${_patchlevel}
_versiondir="vim${_topver//./}"
pkgver=${_topver}.${_patchlevel}
pkgrel=1
depends=("vim-runtime=${pkgver}-${pkgrel}" 'gpm' 'lua' 'python' 'acl')
conflicts=('vim-minimal' 'vim' 'vim-python3' 'gvim' 'gvim-python3')
arch=('i686' 'x86_64')
license=('custom:vim')
url='http://www.vim.org'
makedepends=('python' 'lua' 'mercurial')
source=(vim-$pkgver.tar.gz::http://github.com/vim/vim/archive/v$pkgver.tar.gz
        vimrc
        archlinux.vim)
sha1sums=('SKIP'
          '15ebf3f48693f1f219fe2d8edb7643683139eb6b'
          '94f7bb87b5d06bace86bc4b3ef1372813b4eedf2')

prepare() {
  cd vim-${pkgver}

  # define the place for the global (g)vimrc file (set to /etc/vimrc)
  sed -i 's|^.*\(#define SYS_.*VIMRC_FILE.*"\) .*$|\1|' \
    src/feature.h
  sed -i 's|^.*\(#define VIMRC_FILE.*"\) .*$|\1|' \
    src/feature.h

  (cd src && autoconf)

}

build() {
  cd "${srcdir}/vim-${pkgver}"

  ./configure \
    --prefix=/usr \
    --localstatedir=/var/lib/vim \
    --with-features=huge \
    --with-compiledby='Arch Linux' \
    --enable-gpm \
    --enable-acl \
    --with-x=no \
    --disable-gui \
    --enable-multibyte \
    --enable-cscope \
    --enable-netbeans \
    --enable-perlinterp \
    --disable-pythoninterp \
    --enable-python3interp \
    --disable-rubyinterp \
    --enable-luainterp

  make
}

package() {
  cd "${srcdir}/vim-${pkgver}"
  make -j1 VIMRCLOC=/etc DESTDIR="${pkgdir}" install

  # provided by (n)vi in core
  rm "${pkgdir}"/usr/bin/{ex,view}

  # delete some manpages
  find "${pkgdir}"/usr/share/man -type d -name 'man1' 2>/dev/null | \
    while read _mandir; do
    cd ${_mandir}
    rm -f ex.1 view.1 # provided by (n)vi
    rm -f evim.1    # this does not make sense if we have no GUI
  done

  # Runtime provided by runtime package
  rm -r "${pkgdir}"/usr/share/vim

  # license
  install -Dm644 runtime/doc/uganda.txt \
    "${pkgdir}"/usr/share/licenses/${pkgname}/license.txt
}
# vim:set sw=2 sts=2 et:
