# Maintainer: Aetf <aetf at unlimitedcodeworks.xyz>
# Contributor: Lukas Fleischer <archlinux at cryptocrack dot de>
# Contributor: Mateusz Herych <heniekk@gmail.com>
# Contributor: Thomas Mader <thezema@gmail.com>
# Contributor: Vinay S Shastry <vinayshastry@gmail.com>
# Contributor: tardo <tardo@nagi-fanboi.net>

pkgname=astyle-patched
pkgver=2.05.1
_pkgname=astyle
provides=astyle
conflicts=astyle
pkgrel=1
pkgdesc='A free, fast and small automatic formatter for C, C++, C#, and Java source code, with KDE patches.'
arch=('i686' 'x86_64')
url='http://sourceforge.net/projects/astyle/'
license=('LGPL')
depends=('gcc-libs')
source=("http://downloads.sourceforge.net/sourceforge/astyle/${_pkgname}_${pkgver}_linux.tar.gz"
        "astyle_qt.patch"
        "astyle_break_struct.patch"
        "astyle_comma.patch")
md5sums=('4142d178047d7040da3e0e2f1b030a1a'
         '8cd7d40530e7f740648c0c883928e45e'
         '5ff3b83ff8df8ec8184ccb4d38745075'
         '65dd6687c300c2f7652b87b9da654671')

build() {
  cd "$srcdir/$_pkgname"
  msg2 "Applying astyle_qt.patch"
  patch -Np1 -i ../astyle_qt.patch
  msg2 "Applying astyle_break_struct.patch"
  patch -Np1 -i ../astyle_break_struct.patch
  msg2 "Applying astyle_comma.patch"
  patch -Np1 -i ../astyle_comma.patch
  cd "$srcdir/$_pkgname/build/gcc"
  make release shared
}

package() {
  cd "$srcdir/$_pkgname/build/gcc"

  install -Dm0755 bin/astyle "$pkgdir/usr/bin/astyle"
  install -Dm0755 "bin/libastyle-${pkgver}.so" "$pkgdir/usr/lib/libastyle-${pkgver}.so"

  # install documentation (implements FS#23290)
  install -d "$pkgdir/usr/share/doc/$_pkgname/"
  for f in ../../doc/*.html; do
    install -m0644 "$f" "$pkgdir/usr/share/doc/$_pkgname/"
  done
}
