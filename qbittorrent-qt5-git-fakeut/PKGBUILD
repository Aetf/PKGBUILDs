# Maintainer: Aetf
# Contributor: anex
# Contributor: Sevenseven
# Contributor: Ner0

pkgname=qbittorrent-qt5-git
pkgver=3.2.0.5665
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt5 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar')
makedepends=('boost')
optdepends=('python: needed for search')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=qbittorrent.install
#source=('git://github.com/qbittorrent/qBittorrent.git'
source=('git+file:///home/aetf/Develop/Git/qBittorrent'
        'fake-ut-peerid-and-ua.patch'
        )
md5sums=('SKIP'
         '40c7546ecf25ca025fe86a0ab1d8f530'
         )
options=(debug)

pkgver() {
  cd qBittorrent
  #echo $(cat version.pri | grep -o '[0-9]..*[0-9]').$(git rev-list --count master)
  awk '/^VER_MAJOR/{X=$NF}/^VER_MINOR/{Y=$NF}/^VER_BUGFIX/{Z=$NF}END{printf "%d.%d.%d.",X, Y, Z}'\
	  version.pri
  git rev-list --count master
}

prepare() {
  cd qBittorrent
# Spoof uTorrent [ change to 'no' to disable ]
  _utspoof='yes'

  if [ $_utspoof = 'yes' ]; then
    msg2 "Patching fake-ut-peerid-and-ua.patch"
    patch -p1 < ../fake-ut-peerid-and-ua.patch
  fi
}

build() {
  cd qBittorrent
  ./configure --prefix=/usr \
              --with-qt5 \
              --enable-debug
  make
}

package() {
  cd qBittorrent
  make INSTALL_ROOT="$pkgdir/" install
}
