# Maintainer: Aetf
# Contributor: anex
# Contributor: Sevenseven
# Contributor: Ner0

pkgname=qbittorrent-qt5-git
pkgver=3.3.0.5787
pkgrel=2
pkgdesc="A bittorrent client written in C++ / Qt5 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar>=1.0.0')
makedepends=('git' 'boost')
optdepends=('python: needed for search')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=qbittorrent.install
source=('git://github.com/qbittorrent/qBittorrent.git'
        'fake-ut-peerid-and-ua.patch'
        'make-first-col-movable.patch'
        )
sha256sums=('SKIP'
         'cf64f7de2be98fc6d9d91839f780fa898ab4d5336eb82f02b1d494eac4682fc5'
         '483ae656854c55994632f048d67528af6990dd1360574785c3b2af07119ff7bc'
         )
options=(debug)

pkgver() {
  cd qBittorrent
  #echo $(cat version.pri | grep -o '[0-9]..*[0-9]').$(git rev-list --count master)
  awk '/^VER_MAJOR/{X=$NF}/^VER_MINOR/{Y=$NF}/^VER_BUGFIX/{Z=$NF}END{printf "%d.%d.%d.",X, Y, Z}'\
	  version.pri
  git rev-list --count master
}

prepare() {
  cd qBittorrent
# Spoof uTorrent [ change to 'no' to disable ]
  _utspoof='yes'

  if [ $_utspoof = 'yes' ]; then
    msg2 "Applying fake-ut-peerid-and-ua.patch"
    patch -p1 < ../fake-ut-peerid-and-ua.patch
  fi

  msg2 "Applying make-first-col-movable.patch"
  patch -p1 < ../make-first-col-movable.patch
}

build() {
  cd qBittorrent
  ./configure --prefix=/usr \
              --with-qt5 \
              --enable-debug
  make
}

package() {
  cd qBittorrent
  make INSTALL_ROOT="$pkgdir/" install
}
