# Maintainer: Aetf
# Contributor: anex
# Contributor: Sevenseven
# Contributor: Ner0

pkgname=qbittorrent-qt5-git
pkgver=3.2.0.5635
pkgrel=1
pkgdesc="A bittorrent client written in C++ / Qt5 using the good libtorrent library"
arch=('i686' 'x86_64')
url="http://www.qbittorrent.org/"
license=('GPL')
depends=('qt5-base' 'libtorrent-rasterbar')
makedepends=('boost' 'which')
optdepends=('python: needed for search')
conflicts=('qbittorrent')
provides=('qbittorrent')
install=qbittorrent.install
#source=('git://github.com/qbittorrent/qBittorrent.git'
source=('git+file:///home/aetf/Develop/Git/qBittorrent'
        'fake-ut-peerid-and-ua.patch'
        'workaround-negative-ratio.patch'
        )
md5sums=('SKIP'
         '075bced15412c23e9740a10b6c149b6a'
         '7791f2886482cb62b52d8a04fff60841'
         )
options=(debug)

pkgver() {
  cd qBittorrent
  #echo $(cat version.pri | grep -o '[0-9]..*[0-9]').$(git rev-list --count master)
  awk '/^VER_MAJOR/{X=$NF}/^VER_MINOR/{Y=$NF}/^VER_BUGFIX/{Z=$NF}END{printf "%d.%d.%d.",X, Y, Z}'\
	  version.pri
  git rev-list --count master
}

prepare() {
  cd qBittorrent
# Spoof uTorrent [ change to 'no' to disable ]
  _utspoof='yes'

  if [ $_utspoof = 'yes' ]; then
    msg2 "Patching fake-ut-peerid-and-ua.patch"
    patch -p1 < ../fake-ut-peerid-and-ua.patch
  fi
  msg2 "Patching workaround-negative-ratio.patch"
  patch -p1 < ../workaround-negative-ratio.patch
}

build() {
  cd qBittorrent
  ./configure --prefix=/usr \
              --with-qt5 \
              --enable-debug
  make
}

package() {
  cd qBittorrent
  make INSTALL_ROOT="$pkgdir/" install
}
