# Maintainer : Aetf <aetf@unlimitedcodeworks.xyz>
# Contributor : Kevin MacMartin <prurigro@gmail.com>

_pkgbase=syncthing
_cmdname=stdiscosrv
pkgname=syncthing-discosrv
pkgver=0.14.52
pkgrel=1
pkgdesc='Discover server for the syncthing P2P synchronization utility'
url='http://syncthing.net'
license=('MPL')
depends=('glibc')
makedepends=('git' 'go' 'inetutils')
arch=('i686' 'x86_64' 'arm' 'armv6h' 'armv7h')

source=(
  https://github.com/syncthing/syncthing/releases/download/v$pkgver/syncthing-source-v${pkgver}.tar.gz
  "syncthing-discosrv.sysusers"
  "syncthing-discosrv.service"
  "syncthing-discosrv.tmpfiles.conf"
)

sha512sums=('1c832fc47ebe06d1ec90cffb904734ed68e468876907be815da20c77037046f57143f455763572013f4de249cb4c14e5a395c8febf1d3ea29c8754352cba05eb'
            '7342861b3dfa0610add5dd47329ec85712c79d18eb718d9bc89596797cca68233a912eeda25ee0299b12656ede15913b0652e8c7872ade2db52e8606d042712c'
            '0735fc7ba8cc345da5a742ba942651f20340b448dffc04cb33d6b153a60761eaadf222399b64454cea364d14ae1e0e6e8ba2ae99eda2b221df4adaba48d190b6'
            'ec0ca45270d424526837e0e4baea160eb08e03ba8eeb8b679d3c6280b9b872a109061905cb8144f0ab60a9390cc556b19b0beec34cd93a3b1f4a35984ec434ef')

prepare() {
  install -d "src/github.com/syncthing"
  mv "${_pkgbase}" "src/github.com/syncthing/${_pkgbase}"
}

build() {
  export GOPATH="${srcdir}" GOROOT_FINAL="/usr/bin"
  cd "${srcdir}/src/github.com/syncthing/${_pkgbase}"
  go run build.go -no-upgrade -version v${pkgver} build ${_cmdname}
}

package() {
  cd "${srcdir}/src/github.com/syncthing/${_pkgbase}"
  install -Dm755 ${_cmdname} "${pkgdir}"/usr/bin/${pkgname}

  # Install the man page
  install -Dm644 man/${_cmdname}.1 "$pkgdir/usr/share/man/man1/$pkgname.1"

  # Install systemd service and tmpfiles config
  install -Dm644 ${srcdir}/$pkgname.service "$pkgdir/usr/lib/systemd/system/$pkgname.service"
  install -Dm644 ${srcdir}/$pkgname.tmpfiles.conf "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
  install -Dm644 ${srcdir}/$pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
}
