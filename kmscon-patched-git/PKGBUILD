# Maintainer: Aetf <7437103@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: David Herrmann <dh.herrmann@googlemail.com>
pkgname=kmscon-patched-git
_gitname=kmscon
pkgver=8.r20.g0b34527
pkgrel=3
pkgdesc='Terminal emulator based on Kernel Mode Setting (KMS), with patches to add underline support and fix margin background color'
arch=(i686 x86_64)
url='http://www.freedesktop.org/wiki/Software/kmscon/'
license=('MIT')
depends=(systemd libdrm mesa libgl pango libxkbcommon xkeyboard-config libtsm-patched)
makedepends=(libxslt docbook-xsl linux-api-headers)
options=(!libtool)
provides=(kmscon)
conflicts=(kmscon)
source=("git+https://github.com/dvdhrm/kmscon.git"
        add-underline.patch
        fix-margin-color.patch)
md5sums=('SKIP'
         'SKIP'
         'SKIP')

pkgver() {
  cd $srcdir/$_gitname
  git describe --long | sed -r "s/^$_gitname-//;s/([^-]*-g)/r\\1/;s/-/./g"
}

prepare() {
  cd $srcdir/$_gitname

  msg2 "Applying add-underline.patch"
  patch -Np1 -i ../add-underline.patch
  msg2 "Applying fix-margin-color.patch"
  patch -Np1 -i ../fix-margin-color.patch

  ./autogen.sh --prefix=/usr \
                --disable-wlterm
}

build() {
  cd $srcdir/$_gitname
  make
}

package() {
  cd $srcdir/$_gitname
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/usr/share/licenses/$pkgname" "$pkgdir/usr/lib/systemd/system"
  cp COPYING "$pkgdir/usr/share/licenses/$pkgname/"
  cp docs/kmscon{,vt@}.service "$pkgdir/usr/lib/systemd/system/"
}
